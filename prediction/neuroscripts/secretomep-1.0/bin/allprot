#! /usr/freeware/bin/gawk -f

# 
# allprot - all about protein sequences
# 
# This script reads protein sequences in FASTA and outputs simple statistical
# information om each sequence, currently:
# 
# 	* sequence length (number of amino acids);
# 	* total number of atoms:
# 	* total number of negatively charged residues (Asp + Glu);
# 	* total number of positively charged residues (Arg + Lys);
# 
# It can easily be extended to include other parameters i.e. amino acid
# or atomic composition etc.
# 
# VERSION:	23 Aug 2004	launch, K. Rapacki
# 


BEGIN {	# atoms in each amino acid
	atoms["A"]=13; atoms["M"]=20;
	atoms["C"]=14; atoms["N"]=17;
	atoms["D"]=16; atoms["P"]=17;
	atoms["E"]=19; atoms["Q"]=20;
	atoms["F"]=23; atoms["R"]=26;
	atoms["G"]=10; atoms["S"]=14;
	atoms["H"]=20; atoms["T"]=17;
	atoms["I"]=22; atoms["V"]=19;
	atoms["K"]=24; atoms["W"]=27;
	atoms["L"]=22; atoms["Y"]=24;

	# negative and positive residues
	negaa["D"]=1; posaa["K"]=1;
	negaa["E"]=1; posaa["R"]=1;
}

# FASTA header line: report and reset
/^>/ {	if (id!="") report(); reset(); id=substr($0,2); next; }

# sequence line: gather data
{
  gsub("[^A-Z]","",$0);
  max=length($0);
  for (i=1; i<=max; i++) {
      el = substr($0,i,1);
      len++;
      totatoms+=atoms[el];
      negatoms+=negaa[el];
      posatoms+=posaa[el];
  }
}

# EOF: report last sequence
END {	report(); }

###############################################################################

function reset() {

  len=0; totatoms=0; negatoms=0; posatoms=0;

  return 0;

}

function report() {

  print ">" id;
  print "\nNumber of amino acids:",len;
  print "\nTotal number of atoms:",totatoms-3*(len-1);
  print "\nTotal number of negatively charged residues (Asp + Glu):",negatoms;
  print "\nTotal number of positively charged residues (Arg + Lys):",posatoms;
  print "";

  return 0;

}

# end of script ###############################################################
